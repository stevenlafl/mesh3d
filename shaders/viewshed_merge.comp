#version 430 core
layout(local_size_x = 16, local_size_y = 16) in;

/* Per-node results (read-only) */
layout(binding = 0, r8ui) uniform readonly  uimage2D uNodeVis;
layout(binding = 1, r32f) uniform readonly  image2D  uNodeSignal;

/* Accumulated merge buffers (read-write) */
layout(binding = 2, r8ui) uniform           uimage2D uMergedVis;
layout(binding = 3, r32f) uniform           image2D  uMergedSignal;
layout(binding = 4, r8ui) uniform           uimage2D uOverlapCount;

uniform ivec2 uGridSize; // (cols, rows)

void main() {
    ivec2 gid = ivec2(gl_GlobalInvocationID.xy);
    if (gid.x >= uGridSize.x || gid.y >= uGridSize.y)
        return;

    uint node_vis = imageLoad(uNodeVis, gid).r;
    if (node_vis == 0u)
        return;

    /* OR visibility */
    imageStore(uMergedVis, gid, uvec4(1, 0, 0, 0));

    /* MAX signal */
    float node_sig  = imageLoad(uNodeSignal, gid).r;
    float merged_sig = imageLoad(uMergedSignal, gid).r;
    if (node_sig > merged_sig) {
        imageStore(uMergedSignal, gid, vec4(node_sig, 0.0, 0.0, 0.0));
    }

    /* Increment overlap */
    uint overlap = imageLoad(uOverlapCount, gid).r;
    imageStore(uOverlapCount, gid, uvec4(overlap + 1u, 0, 0, 0));
}
